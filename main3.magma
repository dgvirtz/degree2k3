// checks for Prop. 8.18
ds := [2,6,6,6]; l:=3;

load "globals.magma";
load "utils.magma";
load "computeCohomology.magma";
load "sextic3.magma";

// the list of all subgroups of G_3 up to conjugation and permutation of the coordinates
// previously generated by ListAllSubgroups in sextic3.magma
for j in [1..#allsubgroupsQ3] do
    //only care here about the (abelian) action over k
    H := MatrixToSubgroup(allsubgroupsQ3[j]) @ isoGPC meet AbGfin;
    
    if not IsTrivial(H) then
        CPic := MyRestriction(CPicGen,H);
        CPicDual := MyRestriction(CPicDualGen,H);
        CDiscT := MyRestriction(CDiscTGen,H);
        CBrl := MyRestriction(CBrlGen,H);
        CM := MyRestriction(CMGen,H);
    
        H1PicDual := CohomologyGroup(CPicDual, 1);
        H1Disc := CohomologyGroup(CDiscT, 1);
        H0Brl := CohomologyGroup(CBrl,0);
        m2 := InducedMapH1(CPicDual, CDiscT, f2*f3);
        m1 := ConnectingMapH0H1(CDiscT,CM,CBrl,f,f5);
    
        A1 := RSpaceToAbelian(H0Brl);
        A2 := RSpaceToAbelian(H1PicDual);
        A3 := RSpaceToAbelian(H1Disc);
        a13 := hom<A1->A3|[A3!Eltseq(x):x in RowSequence(m1)]>;
        a23 := hom<A2->A3|[A3!Eltseq(x):x in RowSequence(m2)]>;
        img := Image(a13) meet Image(a23);
        
        Brtrans := sub<A1|[x @@ a13: x in Generators(img)] cat Setseq(Generators(Kernel(a13)))>;
        
        //check that all cases of nontrivial transcendental 3-torsion have large Galois-invariant Brauer group
        if #Brtrans ne 1 then
            assert #H0Brl eq 9;
        end if;
    end if;
end for;
