// checks for Prop. 8.18
ds := [2,6,6,6]; l:=2;

load "globals.magma";
load "utils.magma";
load "computeCohomology.magma";
load "sextic2.magma";

// the list of all subgroups of G_2 up to conjugation and permutation of the coordinates
// previously generated by ListAllSubgroups in sextic2.magma
for j in [1..#allsubgroupsQ2] do
    //only care here about the (abelian) action over k
    H := MatrixToSubgroup(allsubgroupsQ2[j]) @ isoGPC meet AbGfin;
    
    if not IsTrivial(H) then
        CPic := MyRestriction(CPicGen,H);
        CPicDual := MyRestriction(CPicDualGen,H);
        CDiscT := MyRestriction(CDiscTGen,H);
        CBrl := MyRestriction(CBrlGen,H);
        CM := MyRestriction(CMGen,H);
    
        H1PicDual := CohomologyGroup(CPicDual, 1);
        H1Disc := CohomologyGroup(CDiscT, 1);
        H0Brl := CohomologyGroup(CBrl,0);
        m2 := InducedMapH1(CPicDual, CDiscT, f2*f3);
        m1 := ConnectingMapH0H1(CDiscT,CM,CBrl,f,f5);
    
        A1 := RSpaceToAbelian(H0Brl);
        A2 := RSpaceToAbelian(H1PicDual);
        A3 := RSpaceToAbelian(H1Disc);
        a13 := hom<A1->A3|[A3!Eltseq(x):x in RowSequence(m1)]>;
        a23 := hom<A2->A3|[A3!Eltseq(x):x in RowSequence(m2)]>;
        img := Image(a13) meet Image(a23);
        
        Brtrans := sub<A1|[x @@ a13: x in Generators(img)] cat Setseq(Generators(Kernel(a13)))>;
        
        //check that all subgroups with non-trivial transcendental 2-torsion leave
        //4A_1A_2A_3 or -A_1A_2A_3/2 invariant (implying these are sixth powers)
        if #Brtrans ne 1 then
            auts := RowSequence(allsubgroupsQ2[j][1]);
            assert {ed^(a[1]+a[2]+a[3]+3*a[4]+2*a[5]): a in auts} eq {1}
            or {ed^(a[1]+a[2]+a[3]+5*a[5]): a in auts} eq {1};
        end if;
    end if;
end for;
